grammar fr.lip6.puck.dsl.Puck with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes

generate puck "http://www.lip6.fr/puck/dsl/Puck"

PuckModel:
	(imports=XImportSection)?

	(namedSets+=SetDeclaration
	|	rules+=Rule)*;
//	statements+=Statement*;


RuleSet :
	SetDeclaration | IteratorDef
;

Rule:
	'hide'
	hide=[SetDeclaration]
	'from'
	from=[SetDeclaration]
	';'
;


RuleWithSet:
	'hide'
	hide=[RuleSet]
	'from'
	from=[RuleSet]
	';'
;

Statement : 
	ForLoop | RuleWithSet | Filter
;

Filter : 
	'if' '(' test=XExpression ')' '{'
		body+=Statement*
	'}'
;

IteratorDef : 
	name=ID
;

ForLoop: 
	'for' '(' iter=IteratorDef ':' set=[SetDeclaration] ')'
	'{' 
		(body+=Statement)* 
	'}'	
;

SetDeclaration:
	name=ID '='  nodes=NodeSet
	( 'except' except=NodeSet )? ';'	
;

NodeSet:
	'[' nodes+=NodeReference (',' nodes+=NodeReference)* ']'
;

NodeReference:
	PackageReference | TypeReference  | AllReference | SetReference // | FieldReference | MethodReference
;

SetReference:
	ref=[SetDeclaration] 
;

AllReference:{AllReference}
	'all'
;

TypeReference:
  'type' type=JvmTypeReference
;

PackageReference:
  'pkg' package=QualifiedName
;

